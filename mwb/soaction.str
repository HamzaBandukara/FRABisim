functor SortAction(structure N : SoNAME) : SoACTION =
struct

   structure N = N

   datatype action = Tau
                | Input of N.name
                | Output of N.name

   fun mkstr(Tau) = "t"
     | mkstr(Input(c)) = (N.mkstr c)
     | mkstr(Output(c)) ="'" ^ (N.mkstr c)

   fun eq (Tau,Tau) = true
     | eq (Input(c1),Input(c2)) = N.eq(c1,c2)
     | eq (Output(c1),Output(c2)) = N.eq(c1,c2)
     | eq (_,_) = false

   fun is_tau(Tau) = true
     | is_tau _ = false
   fun is_input(Input _) = true
     | is_input _ = false
   fun is_output (Output _) = true
     | is_output _ = false

   fun mk_tau() = Tau
   fun mk_input n = Input n
   fun mk_output n = Output n

   fun name(Input(n)) = n
     | name (Output(n)) = n

   fun substitute(nn,n,Tau) = Tau
     | substitute (nn,n,a as (Input(m))) =
       if N.eq(n,m) then Input(nn) else a
     | substitute (nn,n,a as (Output(m))) =
       if N.eq(n,m) then Output(nn) else a

end
